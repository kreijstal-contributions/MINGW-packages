_realname=rio
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.1.15
pkgrel=1
pkgdesc="A hardware-accelerated GPU terminal emulator powered by WebGPU (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://github.com/raphamorim/rio"
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-freetype"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-fontconfig")
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz") 
sha256sums=('2e06774a23f9f3def6c4abb3d29ada88ee2f96e88235929251b4e7dd7cd5dcf1')

prepare() {
  cd "${_realname}-${pkgver}" 

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}-${pkgver}"

  cargo build --frozen --release --all-features
}

package() {
  cd "${_realname}-${pkgver}"

  install -Dm0755 -t "${pkgdir}${MINGW_PREFIX}/bin/" "target/release/${_realname}"
  install -Dm0644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/" "LICENSE"
}
