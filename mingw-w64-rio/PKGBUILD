_realname=rio
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.1.14
pkgrel=1
pkgdesc="A hardware-accelerated GPU terminal emulator powered by WebGPU (mingw-w64)"
arch=('any')
mingw_arch=( 'clang64')
url="https://github.com/raphamorim/rio"
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-freetype"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-fontconfig"
             "${MINGW_PACKAGE_PREFIX}-gcc-compat")
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz"
        "mingw-build.patch") 
sha256sums=('9013fdcd65ec64285adb83d18c18310a1e52e37f16183dec47e2fa62d862f2d8'
            '95894173cf17b1bf27e1dcac0edde83fad274a4b2e4fbd632f295ce5f4c31948')

prepare() {
  cd "${_realname}-${pkgver}" 

  patch -Np1 -i "${srcdir}/mingw-build.patch"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}-${pkgver}"

  cargo build --frozen --release --all-features
}

package() {
  cd "${_realname}-${pkgver}"

  install -Dm0755 -t "${pkgdir}${MINGW_PREFIX}/bin/" "target/release/${_realname}"
  install -Dm0644 -t "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/" "README.md"
  install -Dm0644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/" "LICENSE"
}
